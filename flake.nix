{
  # This flake, which I'm now calling The One Flake, describes the entire FAIR Forge monorepo including all its tools.
  # The eventual goal is to make each tool its own flake, as well as each builder, with each tool and builder depending
  # on the root flake.  However we're not there yet, so currently we manage everything through The One Flake.

  description = "FAIR Forge mono-flake";

  inputs = {
    flake-parts.url = "github:hercules-ci/flake-parts";
    flake-root.url = "github:srid/flake-root";
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  };

  outputs =
    inputs@{ self, flake-parts, ... }:
    flake-parts.lib.mkFlake { inherit inputs; } {
      imports = [ inputs.flake-root.flakeModule ];

      systems = [
        "x86_64-linux"
        "aarch64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
      ];

      # most of the flake should go in here
      perSystem =
        {
          config,
          self',
          inputs',
          pkgs,
          system,
          lib,
          ...
        }:
        let
          buildInputs = with pkgs; [
            bashInteractive
            coreutils
            curl
            git
            gnutar
            jq
            just
            lrzip
            perl
            php
            php84Packages.composer
            subversion
            sqlite
            systemfd
            tzdata
            watchexec
            zip
            zstd
          ];

          extensions = with pkgs.php84Extensions; [
            bcmath
            ctype
            curl
            dom
            ds
            ffi
            filter
            gettext
            gmp
            intl
            mbstring
            openssl
            pdo
            pdo_sqlite
            posix
            readline
            simplexml
            sockets
            sodium
            sqlite3
            tokenizer
            xmlwriter

            # full list in /nix/store/nsybw5k5jcqwccbgslfq5psmqh3x3svs-php-with-extensions-8.4.14/lib/php.ini

            # future consideration
            #fileinfo
            #iconv
            #mysqli
            #mysqlnd
            #pcntl
            #pdo_mysql
            #pdo_pgsql
            #pgsql
            #session
            #zip
            #zlib
          ];

          zend-extensions = with pkgs.php84Extensions; [
            opcache
            xdebug
          ];

        in
        {
          devShells.default = pkgs.mkShell {
            inherit buildInputs;

            inputsFrom = [ config.flake-root.devShell ]; # sets $FLAKE_ROOT

            shellHook = ''
              export FAIR_FORGE=$FLAKE_ROOT
              export SELF_DIR=${self'.packages.default}
              export PHP_INI_SCAN_DIR=:${self'.packages.default}
            '';

            # in case $FLAKE_ROOT isn't available, this should also work.
            #
            #     export FAIR_FORGE=$(${lib.getExe config.flake-root.package})
            #
            # Note that the flake root is impure state, and thus should never be set in an attribute or in a file,
            # so technically this makes our flake impure because of all of the dependencies on $FAIR_FORGE.  But since
            # $FAIR_FORGE in the package always points into the nix store for this flake, we can get away with it
            # while still allowing local dev to point to the working copy.
          };

          packages.default = pkgs.stdenv.mkDerivation {
            inherit buildInputs;

            name = "fair-forge";

            src = ./.;

            php-ini =
              let
                get-extension-name = name: builtins.elemAt (builtins.split "-" name) 2; # "php-intl-8.4.13" -> "intl"
                ext-line = ext: "extension = ${ext}/lib/php/extensions/${get-extension-name ext.name}.so";
                zend-ext-line = ext: "zend_extension = ${ext}/lib/php/extensions/${get-extension-name ext.name}.so";
              in
              (map ext-line extensions) ++ (map zend-ext-line zend-extensions);

            buildPhase = ''
              mkdir -p $out

              cat << EOF > $out/php.ini
              ; PHP extensions for FAIR Forge
              ; This file is generated by Nix
              ; Do not edit this file directly
              ; Instead, edit flake.nix and run `nix build` to regenerate this file

              ${lib.concatStringsSep "\n" config.packages.default.php-ini}
              EOF
            '';

            dontInstall = true;
          };

          # invoke with `nix fmt flake.nix`
          formatter = pkgs.nixfmt-rfc-style;
        };

      flake = {
        # system-agnostic flake attributes go here.  we don't have any yet.
      };
    };
}
